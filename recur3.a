#/*# Start of file recur3.a */
#
#/*# MIPSMARK 1.0 1/5/98 Copyright 1998 J. Waldron. */
#
## Question:
##
## Write a function named traverse that will
## do an inorder traverse of a binary tree.
##
##  I have attempted to make the tree defined in the
##  data segment such that 
##
## The argument to travrese is a pointer to
##  a node of the tree, 
##
## A node consists fo 4 words:
##	name - address of string such as "apple"
##	left - pointers to child nodes
##	right    "           "
##	value - an integer, might represent number of apples, for example
##
## The code for traverse could look like:
##
##	if (pointer is null) return 0 	
##	   val1 = traverse   (left)
##		  print_path (name)
##	   val2 = traverse   (right)
##           call store_path
##           return val1 + val2 + value  # sum of all values found so far
##
##
## Output format should follow the pattern:
## ""apple, apricot, orange, peach, pear, pineapple 42"
## 
#/*# Output format must be:		*/
#/*# "apple, apricot, avocado, cantalope, orange, peach, pear, pineapple, star, 407"		*/
#include <stdio.h>       /* for printf in C programs */
#include <stdlib.h>	 /* for exit() in C programs */


#/*##############################################
#					 	#
#		text segment			#
#						#
#################################################

	.text		
       	.globl __start 
__start:		# execution starts here  */

        la $a0,tree
        jal traverse	# Traverse the tree  <<===== YOU
			# Should have printed names in alphabet order
        move  $a0,$v0
        li $v0,1
        syscall         # print value (sum of fruit)
        la $a0,endl
        li $v0,4
        syscall         # print newline
_exit:
        li $v0,10
        syscall         # auf Wiedersehen


#------------------------------------------------
# print_name() - print the items as they are visited in order
#		argument $a0 is address of a string
#------------------------------------------------
print_name:
	li $v0,4
        syscall         # print path[i]
        la $a0,arrow
        li $v0,4
        syscall         # print ", "
        jr $ra


#/* Any changes above this line will be discarded by
# mipsmark. Put your answer between dashed lines. */
#/*-------------- start cut ----------------------- */

#/*  Student's Name:		Account:		*/


## Lin Jensen solution to traverse

traverse:
	bnez	$a0, node_exists  #test for NULL
	   li 	$v0, 0
	   jr 	$ra		#return value of 0 (no appoles in the air)
node_exists:
	sub	$sp, $sp, 16		#save registers
	sw	$ra,  ($sp)
	sw	$s0, 4($sp)     # use for node pointer
	sw	$s1, 8($sp)	# use for value1, returned by traverse left
	move	$s0, $a0
##  Go LEFT
	lw $a0, 4($s0)		# pointer to left child (or null)
	jal  traverse
	move $s1, $v0		# s1 = left sum


##  VISIT          call print_path
	lw	$a0, ($s0)	# name of this node
	jal	print_name

##  Go RIGHT
	lw $a0, 8($s0)		# pointer to right child (or null)
	jal  traverse
## return sum of left, our value, and right (v0)
	add $v0,$v0,$s1		# left sum
	lw $t0, 12($s0)		# our value
	add $v0,$v0,$t0		#now return this
get_out:
	lw	$ra,  ($sp)
	lw	$s0, 4($sp)     # use for node pointer
	lw	$s1, 8($sp)	# use for depth
	add	$sp, $sp, 16		# restore registers
	jr	$ra

#/*
	j __start	#nasty loop if mips program not exited */
#/*--------------  end cut  -----------------------
# Any changes below this line will be discarded by
# mipsmark. Put your answer between dashed lines.

#################################################
#                                               #
#               data segment                    #
#                                               #
#################################################

        .data			#*/
# The binary tree.  Note that each node has four
# words -- a pointer to the name, pointers to
# left and right subtrees, and the integer
# value field, in this case value could be the
# price of one fruit

path:   .space 40

tree:   .word name5, node1, node2, 10
node1:  .word name11, node3, node4, 15
node2:  .word name7, node5, node6, 8
node3:  .word name17, node7, 0, 30
node4:  .word name15, 0, node9, 20
node5:  .word name3, 0, 0, 40
node6:  .word name12, 0, 0, 50
node7:  .word name0, 0, 0, 230
node8:  .word name2, 0, 0, 30  # not used
node9:  .word name1, 0, 0, 4
# these nodes not used
node10: .word name4, 0, 0, 70
node11: .word name16, 0, 0, 60
node12: .word name8, node14, node15, 90
node13: .word name14, 0, 0, 80
node14: .word name9, 0, 0, 91
node15: .word name10, node16, node17, 97
node16: .word name13, 0, 0, 130
node17: .word name6, 0, 0, 35

name0:  .asciiz "apple"
name1:  .asciiz "orange"
name2:  .asciiz "bananna"
name3:  .asciiz "pear"
name4:  .asciiz "plum"
name5:  .asciiz "peach"
name6:  .asciiz "nectarine"
name7:  .asciiz "pineapple"
name8:  .asciiz "grapefruit"
name9:  .asciiz "grape"
name10: .asciiz "melon"
name11: .asciiz "avocado"
name12: .asciiz "star"
name13: .asciiz "mango"
name14: .asciiz "passion"
name15: .asciiz "cantaloupe"
name16: .asciiz "watermelon"
name17: .asciiz "apricot"

endl:   .asciiz "\n"
arrow:  .asciiz ", "
notfound: .asciiz " not found\n"
space:	.asciiz	" "

#
#/*# End of file recur3.a		*/
